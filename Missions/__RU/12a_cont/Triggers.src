every 0$1+0$0.1 do // Alliance base side change
var filter,un;
begin
  if IsInArea(Burlak,SeeBaseArea) then
  begin
    filter:=FilterAllUnits([f_side,neutral]);
    for un in filter do
      setside(un,you);
    BurlakReachedBase:=true;
    disable;
  end
  else                                                               
    enable;
end;

every 0$1+0$0.2 do // Alliance build base
var filter,un,skill,inzenyr;
begin
  if IsInArea(Burlak,StartBuildArea) then
  begin
    DisableExclamations;
    filter:=FilterAllUnits([f_side,neutral]);
    skill:=0;
    for un in filter do
      if GetSkill(un,skill_engineering)>=skill then inzenyr:=un;
    ComStop(inzenyr);
    if GetClass(inzenyr)<>classtype_engineer then
    begin
      filter:=FilterAllUnits([[f_btype,b_warehouse],[f_side,neutral]]);
      ComEnterUnit(inzenyr,filter[1]);
      Wait(10);
      AddComChangeProfession(inzenyr,classtype_engineer);
      Wait(10);
      AddComExitBuilding(inzenyr);
    end;
    Wait(10);
    AddComBuild(inzenyr,b_oil_power,126,17,5);
    EnableExclamations;
    disable;
  end
  else
    enable;
end;

every 0$10+0$0.3 marked 1 do // Creating Ru_Straz
var i,bazukr;
begin
  if straz<bazooker then
  begin
    bazooker:=bazooker+1;
    while straz<bazooker do
    begin
      uc_side:=rus;
      uc_nation:=nation_russian;
      PrepareHuman(sex_male,class_bazooker, 4+difficulty);
      hc_name:='';
      bazukr:=CreateHuman;
      AddMcUnitsSpec(rus_mcrep_id,bazukr,1);
      straz:=straz^bazukr;
      PlaceUnitArea(bazukr,EnterArea,false);
      ComAgressiveMove(bazukr,66,48);
    end;
  end;
 // enable;
end;

every 0$5+0$0.4 marked 2 do // Moving Ru_straz
var un;
begin
  if (0+straz)<>0 then
  begin
    for un in straz do
    begin
      if NOT HasTask(un) AND GetLives(un)>601 then
      begin
        if GetY(un)>60 then
        begin
          ComAgressiveMove(un,66,48);
          AddComAgressiveMove(un,63,74);
          AddComAgressiveMove(un,73,90);
          AddComAgressiveMove(un,76,95);     
          AddComAgressiveMove(un,72,95);
          AddComAgressiveMove(un,50,73);
        end
        else
        begin
          ComAgressiveMove(un,73,90);
          AddComAgressiveMove(un,76,95);
          AddComAgressiveMove(un,72,95);
          AddComAgressiveMove(un,50,73);
          AddComAgressiveMove(un,63,74);
          AddComAgressiveMove(un,66,48);
        end;
      end;
    end;    
  end;
  enable;
end;

every 0$1+0$0.5 do // Dialogues on alliance base etc.
var filter,un;
begin
  if IsInArea(Burlak,BaseArea) then
  begin
    disabled5:=true;
    D_GosStart;
    Wait(0$5);
    D_Situation;
    ChangeMissionObjectives('MHold');
    disable;
    enable(333); // Creating Rus. attack vehicles
    enable(334); // Waiting for attack (BeforeWaterArea)
    enable(337); // Attack on Gossudarov and scientists
  end
  else
    enable;
end;


var disabled3,disabled5;

every 0$0.5 marked 3 do // Gossudarov guiding
begin
  if IsSelected(Gossudarov)=true then
  begin
    if WasSelected=false then
    begin
      QueryQGO;
      WasSelected:=true;
    end;
  end
  else
    WasSelected:=false;
  if NOT disabled3 then enable;
end;

EXPORT function QueryQGO;
var res,filter,Goss_cargo,pocet,un,t;
begin
  res:=Query('QGO');
  if res=1 then
  begin
    disabled3:=true;
    D_GO1;
    ChangeMissionObjectives('MGuide');

{
cargo pronasledujici gosse
pokud je gossovo cargo dead a artefakt vypadne
nekolik vojaku bude u nej, nekolik se jich bude snazit vedce killnout
prijede cargo a artefakt si odveze
pokud se podari cargo odvezt, hra konci Loss Artefact
}

    filter:=FilterUnitsInArea(BluekherCenterArea,[[f_type,unit_vehicle],[f_weapon,ru_cargo_bay]]);
    if filter=0 then
    begin
      ComMoveXY(Gossudarov,57,98);
      repeat
        Wait(0$1);
      until NOT HasTask(Gossudarov);
      RemoveUnit(Gossudarov);
      uc_side:=ally;
      uc_nation:=nation_russian;
      uc_direction:=5;

      vc_chassis:=ru_medium_wheeled;
      vc_engine:=engine_combustion;
      vc_control:=control_manual;
      vc_weapon:=ru_cargo_bay;
      Goss_cargo:=CreateVehicle;
      PlaceHumanInUnit(Gossudarov,Goss_cargo);
      Wait(0$5);
      PlaceUnitArea(Goss_cargo,EnterArea,False);
    end
    else
      ComEnterUnit(Gossudarov,filter[1]);

    AddComGet(Gossudarov,FindArtifact(4)[1],FindArtifact(4)[2]);
    Wait(0$5);

    t := 0$50;

    repeat
     wait(0$1);
     t := t - 0$1;
     AddComMoveXY(Gossudarov, 27, 47);

    until not FilterUnitsInArea(BluekherArea, [[f_side, 6], [f_weapon, ru_cargo_bay]]) or not t;

    ComExitBuilding(scientists);
    AddComMoveXY(scientists, 27, 47);

    for un in scientists do
      AddComSailEvent(un,un);

    player_units:=player_units union scientists union Gossudarov;
    enable(4); // Scientists in ExitArea
    enable(7); // Loss Artefact checking
    enable(8); // Attack on Scientists
    enable(9); // YouLost('Artefact') checking
  end;
end;

every 0$1+0$0.6 marked 4 do // Scientists in ExitArea
var un;
begin
  for un in scientists do
  begin
    if IsInArea(un,ExitArea)then
    begin
      scientists_saved:=scientists_saved union un;
      RemoveUnit(un);
    end;
    if IsDead(un) then
      scientists_dead:=scientists_dead union un;
  end;

{
  debug_strings:=['(IsInArea(Gossudarov,ExitArea) '&IsInArea(Gossudarov,ExitArea),
                    'IsInArea(IsInUnit(Gossudarov),ExitArea) '&IsInArea(IsInUnit(Gossudarov),ExitArea),
                    'scientists_saved '&scientists_saved,
                    'scientists_dead '&scientists_dead,
                    'GetSide(Artifact_cargo) '&GetSide(Artifact_cargo),
                    'IsInArea(Artifact_cargo,ExitArea) '&IsInArea(Artifact_cargo,ExitArea),
                    'GetCargo(Artifact_cargo,mat_artifact) '&GetCargo(Artifact_cargo,mat_artefact)];
}
  if (IsInArea(Gossudarov,ExitArea) OR IsInArea(IsInUnit(Gossudarov),ExitArea))
     AND scientists_saved+scientists_dead=scientists
     AND GetSide(Artifact_cargo)=you
     AND IsInArea(Artifact_cargo,ExitArea)
     AND GetCargo(Artifact_cargo,mat_artifact)=40 then
  begin
    D_SeeYou;
    TheEnd;
  end
  else
    enable;
end;

var Attack1,Attack2,Attack3;

every 0$1+0$0.8 marked 5 do // Pursuers guiding
var i;
begin
  if IsInArea(Burlak,Attack1Area) OR IsInArea(Masha,Attack1Area) then
  begin
    Wait(0$3);
    Attack1:=true;
  end;

  if IsInArea(Burlak,Attack2Area) OR IsInArea(Masha,Attack2Area) then
  begin
    Wait(0$3);
    Attack2:=true;
  end;

  if IsInArea(Burlak,Attack3Area) OR IsInArea(Masha,Attack3Area) then
  begin
    Wait(0$3);
    Attack3:=true;
  end;

  if Attack1 AND NOT Attack2 AND NOT Attack3 then
    for i:=pursuers_base+1 to pursuers_base+pursuers do
    begin
      ComAttackUnit(pursuer[i],Burlak);
      Wait(2);
    end;

  if Attack1 AND Attack2 AND NOT Attack3 then
    for i:=pursuers_base+1 to pursuers_base+2*pursuers do
    begin
      ComAttackUnit(pursuer[i],Burlak);
      Wait(2);
    end;

  if Attack1 AND Attack2 AND Attack3 then
    for i:=pursuers_base+1 to pursuers_base+3*pursuers do
    begin
      ComAttackUnit(pursuer[i],Burlak);
      Wait(2);
    end;

  for i:=1 to pursuers_base do
  begin
    ComAttackUnit(pursuer[i],Burlak);
    Wait(2);
  end;

  if NOT disabled5 then enable;
end;

every 0$1 marked 6 do // Goss_time countdown
begin
  if Goss_time>0 then
  begin
    display_strings:=['#Ru12a-1',Goss_time];
    Goss_time:=Goss_time-0$1;
    enable;
  end
  else
  begin
    display_strings:='';
    enable(3); // Gossudarov guiding
  end;
end;

every 0$1 marked 7 do // Loss Artefact checking
begin
  if FindArtifact(4) then
  begin
    ComGet(Rus_Cargo,FindArtifact(4)[1],FindArtifact(4)[2]);
    Wait(3);
    AddComMoveToArea(Rus_Cargo,EnterArea);
  end;
  enable;
end;

var Rus_Cargo;

every 0$1 marked 8 do // Attack on Scientists
var i,clovek,sc_utok,un;
begin
  Wait(pausa);

  sc_utok:=[];
  for i:=1 to 2+difficulty do
  begin
    uc_side:=rus;
    uc_nation:=nation_russian;
    uc_direction:=5;

    case Rand(1,3) of
      1:PrepareHuman(sex_male,class_soldier,Rand(4,8));
      2..3:PrepareHuman(sex_male,class_bazooker,Rand(3,7));
    end;

    hc_name:='';
    clovek:=CreateHuman;
    PlaceUnitArea(clovek,EnterArea,false);
    Wait(0$2);
    ComMoveXY(clovek,50,84);
    Wait(2);
    AddComAttackUnit(clovek,Artifact_cargo);
    sc_utok:=sc_utok^clovek;
  end;

  Wait(0$10);
  if NOT IsOK(Rus_Cargo) then
  begin
    uc_side:=rus;
    uc_nation:=nation_russian;
    uc_direction:=5;

    vc_chassis:=ru_medium_tracked;
    vc_control:=control_computer;
    vc_engine:=engine_siberite;
    vc_weapon:=ru_cargo_bay;

    Rus_Cargo:=CreateVehicle;
    PlaceUnitArea(Rus_Cargo,EnterArea,false);
    Wait(0$0.5);
    ComMoveXY(Rus_Cargo,55,84);
  end;

  while sc_utok AND IsOK(Rus_Cargo) do
  begin
    ComAttackUnit(sc_utok,Artifact_cargo);
    Wait(0$2);
    for un in sc_utok do
      if NOT IsOK(un) then
        sc_utok:=sc_utok diff un;
  end;
  Wait(0$5);
  enable;
end;

every 0$3 marked 9 do // YouLost('Artefact') checking
begin
  if IsInArea(Rus_Cargo,BluekherCenterArea) AND (GetCargo(Rus_Cargo,mat_artifact)=40) then
    YouLost('Artefact');
  enable;
end;

// **********************    EVENTS   ********************************

on UnitDestroyed(human) do
var i, un;
begin
  if human=Burlak then YouLost('Burlak');
  if human=Gossudarov then YouLost('Gossudarov');
  if human=Masha then lost_masha:=true;
  if human in straz then
  begin
    straz:=straz diff human;
    RemoveMcUnitsSpec(rus_mcrep_id,human,1);
    killed_russians:=killed_russians+1;
  end;

  if GetSide(human) = 3 and human in attackGroup then
  begin
       for i in attackGroup do
           if i = human then
              attackGroup = attackGroup diff i;
  end;
end;

on SailEvent(num) do
begin
SetSide(num,you);
end;

on ArtifactLoaded(un,size) do
begin
  if GetSide(un)<>rus then Artifact_cargo:=un;
end;

Every 0$1 trigger FilterAllUnits([[f_side, 6], [f_type, unit_vehicle], [f_distxy, 27, 47, 6]]) do
begin
SetSide(Gossudarov, 7);
SetSide(IsInUnit(Gossudarov), 7);
End;






